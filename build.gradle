plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

def dependencyVersions = [
        dropwizard: '1.3.5'
]

dependencies {
    compile "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard",
            'net.logstash.logback:logstash-logback-encoder:4.11',
            'ch.qos.logback:logback-access:1.2.3'

    testCompile 'junit:junit:4.11',
            'org.hamcrest:hamcrest-library:1.3',
            'org.mockito:mockito-core:1.9.5',
            'com.github.stefanbirkner:system-rules:1.16.1',
            "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard"
}

test {
    testLogging {
        events "skipped", "failed", "standardError"
        showCauses true
        showExceptions true
        showStackTraces true
        exceptionFormat "full"

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
    module {
        //if you love browsing Javadoc
        downloadJavadoc = true

        //and hate reading sources :)
        downloadSources = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'uk.gov.ida'
            artifactId 'dropwizard-logstash'
            version "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            name = 'dropwizard-logstash'
            url = "file://${buildDir}"
        }
    }
}
task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}
